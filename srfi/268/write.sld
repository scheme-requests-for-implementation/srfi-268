(define-library (srfi 268 write)
  (export write-array)
  (import (scheme base)
	  (scheme case-lambda)
          (scheme write)
          (srfi 231)
          )
  (begin
    ;;; Error reporting

    (define (check-arg who pred x)
      (unless (pred x)
	(error (string-append who ": invalid argument")
	       x)))

    ;; Associates the storage classes defined by SRFI 231 with
    ;; the appropriate array tags.
    (define storage-classes
      `((,char-storage-class . char)
        (,s8-storage-class . s8)
	(,s16-storage-class . s16)
	(,s32-storage-class . s32)
	(,s64-storage-class . s64)
	(,u1-storage-class  . u1)
	(,u8-storage-class  . u8)
	(,u16-storage-class . u16)
	(,u32-storage-class . u32)
	(,u64-storage-class . u64)
	(,f8-storage-class  . f8)
	(,f16-storage-class . f16)
	(,f32-storage-class . f32)
	(,f64-storage-class . f64)
	(,c64-storage-class . c64)
	(,c128-storage-class . c128)))

    (define (write-tag array)
      (display "#a")
      ;; Try to determine the rest of the tag from *array*'s storage
      ;; class.  This is far from foolproof.
      (cond ((assv (array-storage-class array)
		   storage-classes) =>
	      (lambda (p)
		(display (cdr p))))
	    ;; Unknown or generic storage class
	    (else #t)))

    (define (write-bounds array)
      (let* ((iv (array-domain array))
	     (dimension (interval-dimension iv))
	     (write-dim-bounds
	      (lambda (d)
		(display "(")
		(display (interval-lower-bound iv d))
		(display " ")
		(display (interval-upper-bound iv d))
		(display ")"))))
	(display "(")
	;; Print bounds pair for each dimension of iv.
	(do ((i 0 (+ i 1)))
	    ((= i dimension))
	  (write-dim-bounds i)
	  (when (< i (- dimension 1))
	    (display " ")))
	(display ")")))

    (define (write-contents array)
      (display (array->list* array)))

    (define write-array
      (case-lambda
        ((array port)
	 (check-arg "write-array" output-port? port)
	 (parameterize ((current-output-port port))
	   (write-array array)))
	((array)
	 (check-arg "write-array" array? array)
	 (write-tag array)
	 (write-bounds array)
	 (display " ")
	 (write-contents array))))
  ))
